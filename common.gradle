import java.nio.file.Files

android {
    compileSdk 32

    defaultConfig {
        applicationIdSuffix pkgNameSuffix
        minSdk 21
        targetSdk 32
        versionCode extVersionCode
        versionName "1.0." + "$extVersionCode"
        setProperty("archivesBaseName", "anytv-extensions-$pkgNameSuffix-v$versionName")

        manifestPlaceholders = [
            appName : "AnyTV: $extName",
            extClass: extClass,
        ]
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dependenciesInfo {
        includeInApk = false
    }

    buildFeatures {
        // Disable unused AGP features
        aidl false
        renderScript false
        resValues false
        shaders false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }


    task extInfo()  {
        doLast {
            new File("$projectDir/release", "extension-info.json").text = """
{
    "name": "AnyTV: $extName",
    "package": "com.hajaulee.anytv.extension.$pkgNameSuffix",
    "version": "1.0.$extVersionCode",
    "file": "${rootDir.toPath().relativize(projectDir.toPath()).toString().replace("\\", "/")}/release/anytv-extensions-$pkgNameSuffix-v1.0.${extVersionCode}-release.apk"
}
""".trim()

            List<String> extsInfo = new ArrayList<>()
            new File(rootDir, "extensions").eachDir { dir ->
                dir.eachDir { subdir ->
                    extsInfo.add(new File("extensions/${dir.name}/${subdir.name}/release/extension-info.json").text)
                }
            }
            new File(rootDir, "extensions.json").text = "[" + String.join(",\n", extsInfo) + "]"
        }
    }

    afterEvaluate {
        packageRelease.finalizedBy(extInfo)
    }
}

dependencies {
    implementation(project(":common"))
    implementation 'org.jsoup:jsoup:1.13.1'
}